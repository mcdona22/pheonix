plugins {
    id 'java'
    id 'org.springframework.boot' version '4.0.0'
    id 'io.spring.dependency-management' version '1.1.7'
    id 'jacoco'

}

group = 'com.mcdona22'
version = '0.0.1-SNAPSHOT'
description = 'Demo project for Spring Boot'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(25)
    }
}


configurations {
    mockitoAgent
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-data-rest'
    implementation 'org.springframework.boot:spring-boot-starter-restclient'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-webmvc'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    runtimeOnly 'com.h2database:h2'
    compileOnly 'org.projectlombok:lombok'
    testCompileOnly 'org.projectlombok:lombok'
    implementation("javax.servlet:javax.servlet-api:4.0.1")

    runtimeOnly 'org.springframework.boot:spring-boot-devtools'
    annotationProcessor 'org.projectlombok:lombok'
    testAnnotationProcessor 'org.projectlombok:lombok'
    testImplementation('org.springframework.boot:spring-boot-starter-test')
    testImplementation 'org.springframework.boot:spring-boot-starter-data-rest-test'
    testImplementation 'org.springframework.boot:spring-boot-starter-restclient-test'
    testImplementation 'org.springframework.boot:spring-boot-starter-validation-test'
    testImplementation 'org.springframework.boot:spring-boot-starter-webmvc-test'
    testImplementation "org.mockito:mockito-core:5.+"
    testImplementation("org.junit.jupiter:junit-jupiter-params:6.0.1")

    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

}

tasks.named('test') {
    jvmArgs("-XX:+EnableDynamicAgentLoading")
    useJUnitPlatform()
    finalizedBy jacocoTestReport
    reports {
        junitXml.required = true
        html.outputLocation = layout.buildDirectory.dir('reports/html/unit-tests')
        junitXml.outputLocation = layout.buildDirectory.dir('reports/xml/unit-tests')
    }
}

jacocoTestReport {
    dependsOn test
    reports {
        xml.required = true
        csv.required = false
        html.outputLocation = layout.buildDirectory.dir('reports/html/code-coverage')
    }
}

jacoco {
    toolVersion = "0.8.13"
    reportsDirectory = layout.buildDirectory.dir('reports/xml/code-coverage')
}

tasks.register('integrationTest', Test) {
    // This tells the task to use the same setup as the default 'test' task
    testClassesDirs = sourceSets.test.output.classesDirs
    classpath = sourceSets.test.runtimeClasspath

    // Include only tests with the 'integration' tag
    useJUnitPlatform {
        includeTags 'integration'
    }
    // Optional: Ensure the standard 'test' task excludes them if you want
    // the 'test' task to only run fast unit tests.
    // excludeTags 'integration'
}

tasks.register('unitTest', Test) {
    testClassesDirs = sourceSets.test.output.classesDirs
    classpath = sourceSets.test.runtimeClasspath

    useJUnitPlatform {
        excludeTags 'integration'
    }
}

tasks.named('bootRun') {
    // Example 1: Use the 'dev' Spring Profile by default
    systemProperty 'spring.profiles.active', 'dev'

    // Example 2: Run the server on a custom port
    systemProperty 'server.port', '9090'

    // Example 3: Add extra JVM arguments (e.g., for debugging)
    jvmArgs = [
            // '-Xmx2048m', // Max memory
            // '-Xdebug', // Enable remote debugging
            // '-Xrunjdwp:server=y,transport=dt_socket,address=5005,suspend=n'
    ]
}


